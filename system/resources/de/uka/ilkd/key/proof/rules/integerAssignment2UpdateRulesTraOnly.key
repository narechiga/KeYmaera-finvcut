// This file is part of KeY - Integrated Deductive Software Design
// Copyright (C) 2001-2007 Universitaet Karlsruhe Germany
//                           Universitaet Koblenz-Landau, Germany
//                         and Chalmers University of Technology, Sweden
//
// The KeY system is protected by the GNU General Public License. 
// See LICENSE.TXT for details.
//


/**
  READ FIRST BEFORE EDITING:
    This file contains all rules moving an integer related assignment
  from the inside of a traOnly modality to the logic (as an update). The Java
  operators are therefore mapped to their generic logic
  function. These generic functions (java...) are later interpreted as
  mathematical function dependent on the chosen Java semantics.

  In case of changes PLEASE preserve the files intended meaning. If
  you want to test some rules before implementing them completely,
  please create a new file and include it into the standardRules.key

  Files:
     // rules treating Java programs

     integerAssignment2UpdateRules.key: all integer related assignment
                                        rules for normal assignments      
     integerAssignment2UpdateRulesTout.key: all integer related assignment
                                        rules for throughout assignments      
     integerAssignment2UpdateRulesTraOnly.key: all integer related assignment
                                        rules for traonly assignments

     integerCommonRules.key: integer semantics independent rules which 
         transform programs

     // rules treating integer semantics sensitive functions 

     intRules.key: rules interpreting the generic translation
        functions 'java...' faithfully according to the Java Language
        Specification (sound, but a lot of divisions and modulo
        computation need to be performed)

     intRulesIgnoringOverflow.key: rules interpreting the generic translation
        functions 'java...' without overflow (not sound)  

     intRulesArith.key: rules interpreting the generic translation
        functions 'java...' as in intRulesIgnoringOverflow.key, but 
        requires to show absence of overflow or that the property to
        be proven does not depend on the value of an expression where
        an overflow occured (sound, but incomplete)
   
     // rules treating integer semantics insensitive functions 

     integerSimplificationRules.key: rules for arithmetic functions
         that do not depend on the chose integer semantics
        
*/

\schemaVariables {
  \modalOperator { diamond_tra, box_tra, throughout_tra, diamond_susp, 
                   box_susp, throughout_susp } #traonly;

  \program SimpleExpression #se, #se0, #se1 ;
  \program JavaByteExpression #seByte, #seByte0, #seByte1 ;
  \program JavaShortExpression #seShort, #seShort0, #seShort1 ;
  \program JavaIntExpression #seInt, #seInt0, #seInt1 ;
  \program JavaLongExpression #seLong, #seLong0, #seLong1 ;
  \program JavaCharByteShortIntExpression #seCharByteShortInt, 
      #seCharByteShortInt0, #seCharByteShortInt1; 
  \program JavaByteShortExpression #seByteShort;

  \program Variable #loc ;

  \formula post;
}

\rules(programRules:Java, transactions:transactionsOn, transactionAbort:abortOn) {

//------------------------------------------------------------------------
// ----------- Rules for binary arithmetic operators 
//------------------------------------------------------------------------

  //------------- multiplication

  assignmentMultiplicationIntTra  { 
      \find (\modality{#traonly}{.. 
                 #loc = #seCharByteShortInt0 * #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith ({#shadowed(#loc):=
                       javaMulInt(#shadowed(#seCharByteShortInt0),
                                  #shadowed(#seCharByteShortInt1))}
                          \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };

  assignmentMultiplicationLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seCharByteShortInt * #seLong; 
             ...}\endmodality(post)) 
     \replacewith (
          {#shadowed(#loc):=javaMulLong(#shadowed(#seCharByteShortInt),
                                        #shadowed(#seLong))}
               \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "multiplication"
  };

  assignmentMultiplicationLongTra2 { 
      \find (\modality{#traonly}{.. 
       	         #loc=#seLong * #seCharByteShortInt; 
             ...}\endmodality(post)) 
      \replacewith ({#shadowed(#loc):=javaMulLong(#shadowed(#seLong),
					          #shadowed(#seCharByteShortInt))}
 		         \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };

  assignmentMultiplicationLongTra3  { 
      \find(\modality{#traonly}{.. 
                #loc=#seLong0 * #seLong1; 
            ...}\endmodality(post)) 
      \replacewith({#shadowed(#loc):=
			javaMulLong(#shadowed(#seLong0),
			            #shadowed(#seLong1))}
                     \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "multiplication"
  };

 
  //------------- division

  assignmentDivisionIntTra  { 
      \find(\modality{#traonly}{.. 
                #loc=#seCharByteShortInt0 / #seCharByteShortInt1; 
            ...}\endmodality(post)) 
      \replacewith ((#shadowed(#seCharByteShortInt1)!=0 -> 
           ({#shadowed(#loc):=javaDivInt(#shadowed(#seCharByteShortInt0), 
	   		                 #shadowed(#seCharByteShortInt1))}
                 \modality{#traonly}{.. ...}\endmodality(post)))
           &
           (#shadowed(#seCharByteShortInt1)=0 -> 
               \modality{#traonly}{.. 
                   throw new java.lang.ArithmeticException(); 
               ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLongTra  { 
      \find(\modality{#traonly}{.. 
                #loc=#se / #seLong; 
            ...}\endmodality(post)) 
      \replacewith ((#shadowed(#seLong)!=0 -> 
             ({#shadowed(#loc):=javaDivLong(#shadowed(#se), 
                                            #shadowed(#seLong))}
                \modality{#traonly}{.. ...}\endmodality(post)))  &
             (#shadowed(#seLong)=0 -> \modality{#traonly}{.. 
                   throw new java.lang.ArithmeticException(); 
             ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

  assignmentDivisionLongTra2  { 
      \find(\modality{#traonly}{.. 
                #loc=#seLong / #seCharByteShortInt; 
            ...}\endmodality(post)) 
      \replacewith(
           (#shadowed(#seCharByteShortInt)!=0 -> 
               ({#shadowed(#loc):=javaDivLong(#shadowed(#seLong), 
	       			              #shadowed(#seCharByteShortInt))}
                     \modality{#traonly}{.. ...}\endmodality(post))) 
          &
           (#shadowed(#seCharByteShortInt)=0 -> 
               \modality{#traonly}{.. 
                    throw new java.lang.ArithmeticException(); 
                ...}\endmodality(post)))
      \heuristics(executeIntegerAssignment)
      \displayname "division"
  };

    
  //------------- modulo
  // a modulo operation cannot cause overflow since the magnitude of the result is always
  // less than the magnitude of the divisor (according to Java Lang Spec)

  assignmentModuloTra {
      \find(\modality{#traonly}{..
                #loc=#se0 % #se1; 
            ...}\endmodality(post)) 
     \replacewith (
         (#shadowed(#se1)!=0 -> 
            ({#shadowed(#loc):=javaMod(#shadowed(#se0), 
                                       #shadowed(#se1))}
                 \modality{#traonly}{.. ...}\endmodality(post))) 
        &
          (#shadowed(#se1)=0 -> 
                 \modality{#traonly}{.. 
                     throw new java.lang.ArithmeticException(); 
                 ...}\endmodality(post)))
        \heuristics(executeIntegerAssignment)
        \displayname "modulo"
  };


  //------------- subtraction

  assignmentSubtractionIntTra  { 
      \find (\modality{#traonly}{.. 
                 #loc = #seCharByteShortInt0 - #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=
                javaSubInt(#shadowed(#seCharByteShortInt0),
                           #shadowed(#seCharByteShortInt1))}
                   \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "subtraction"
  };

  assignmentSubtractionLongTra  { 
     \find (\modality{#traonly}{.. 
                #loc=#seCharByteShortInt - #seLong; 
            ...}\endmodality(post)) 
     \replacewith(
          {#shadowed(#loc):=
               javaSubLong(#shadowed(#seCharByteShortInt), #shadowed(#seLong))}
                    \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "subtraction"
  };

  assignmentSubtractionLongTra2 { 
     \find (\modality{#traonly}{.. 
                #loc=#seLong - #seCharByteShortInt; 
            ...}\endmodality(post)) 
     \replacewith(
           {#shadowed(#loc):=javaSubLong(#shadowed(#seLong),
                                         #shadowed(#seCharByteShortInt))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "subtraction"
  };

  assignmentSubtractionLongTra3  { 
      \find (\modality{#traonly}{.. 
                  #loc=#seLong0 - #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=javaSubLong(#shadowed(#seLong0), 
	   		                 #shadowed(#seLong1))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "subtraction"
  };

  //------------- addition

  assignmentAdditionIntTra { 
      \find(\modality{#traonly}{.. 
                #loc=#seCharByteShortInt0 + #seCharByteShortInt1; 
            ...}\endmodality(post)) 
     \replacewith(
         {#shadowed(#loc):=javaAddInt(#shadowed(#seCharByteShortInt0),
                                      #shadowed(#seCharByteShortInt1))}
             \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  assignmentAdditionLongTra { 
     \find(\modality{#traonly}{.. 
               #loc=#seCharByteShortInt + #seLong; 
           ...}\endmodality(post)) 
     \replacewith(
           {#shadowed(#loc):=javaAddLong(#shadowed(#seCharByteShortInt),
                                         #shadowed(#seLong))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  assignmentAdditionLongTra2 { 
     \find(\modality{#traonly}{.. 
               #loc=#seLong + #seCharByteShortInt; 
           ...}\endmodality(post)) 
     \replacewith({#shadowed(#loc):=
           javaAddLong(#shadowed(#seLong),#shadowed(#seCharByteShortInt))}
                       \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "addition"
  };

  //reviewed 04/20/2004, St.S.
  assignmentAdditionLongTra3 { 
      \find(\modality{#traonly}{.. 
                #loc=#seLong0 + #seLong1; 
            ...}\endmodality(post)) 
      \replacewith({#shadowed(#loc):=javaAddLong(#shadowed(#seLong0),
                                                 #shadowed(#seLong1))}
                         \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "addition"
  };

  //------------- binary "and"

  assignmentBitwiseAndIntTra { 
      \find (\modality{#traonly}{.. 
                  #loc=#seCharByteShortInt0 & #seCharByteShortInt1; 
             ...}\endmodality(post)) 
       \replacewith(
             {#shadowed(#loc):=
                    javaBitwiseAndInt(#shadowed(#seCharByteShortInt0),
                                      #shadowed(#seCharByteShortInt1))}
                           \modality{#traonly}{.. ...}\endmodality(post)) 
       \heuristics(executeIntegerAssignment)
       \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLongTra { 
      \find (\modality{#traonly}{.. 
                  #loc=#seCharByteShortInt & #seLong; 
             ...}\endmodality(post)) 
      \replacewith(
            {#shadowed(#loc):=
                 javaBitwiseAndLong(#shadowed(#seCharByteShortInt),
                                    #shadowed(#seLong))}
                             \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLongTra2 { 
        \find (\modality{#traonly}{.. 
                    #loc=#seLong & #seCharByteShortInt; 
               ...}\endmodality(post)) 
        \replacewith (
             {#shadowed(#loc):=
                  javaBitwiseAndLong(#shadowed(#seLong),
                                     #shadowed(#seCharByteShortInt))}
                        \modality{#traonly}{.. ...}\endmodality(post)) 
        \heuristics(executeIntegerAssignment)
        \displayname "bitwiseAnd"
  };

  assignmentBitwiseAndLongTra3 { 
      \find (\modality{#traonly}{.. 
                  #loc=#seLong0 & #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
             {#shadowed(#loc):=javaBitwiseAndLong(#shadowed(#seLong0),
                                                  #shadowed(#seLong1))}
                   \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseAnd"
  };

  //------------- binary "or"

  assignmentBitwiseOrIntTra { 
      \find (\modality{#traonly}{.. 
                  #loc=#seCharByteShortInt0 | #seCharByteShortInt1; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=
                javaBitwiseOrInt(#shadowed(#seCharByteShortInt0),
		                 #shadowed(#seCharByteShortInt1))}
                    \modality{#traonly}{.. ...}\endmodality(post)) 
         \heuristics(executeIntegerAssignment)
         \displayname "bitwiseOr"
  };

  assignmentBitwiseOrLongTra { 
      \find (\modality{#traonly}{.. 
                  #loc=#seCharByteShortInt | #seLong; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=javaBitwiseOrLong(#shadowed(#seCharByteShortInt),
				               #shadowed(#seLong))}
                 \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //reviewed 04/20/2004, St.S.
  assignmentBitwiseOrLongTra2 { 
      \find (\modality{#traonly}{.. 
                  #loc=#seLong | #seCharByteShortInt; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=javaBitwiseOrLong(#shadowed(#seLong),
					       #shadowed(#seCharByteShortInt))}
                 \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //reviewed 04/20/2004, St.S.
  assignmentBitwiseOrLongTra3 { 
      \find (\modality{#traonly}{.. 
                  #loc=#seLong0 | #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
            {#shadowed(#loc):=javaBitwiseOrLong(#shadowed(#seLong0),
                                                #shadowed(#seLong1))}
                 \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseOr"
  };

  //------------- binary "xor"

  assignmentBitwiseXOrIntTra { 
       \find (\modality{#traonly}{.. 
                  #loc=#seCharByteShortInt0 ^ #seCharByteShortInt1; 
              ...}\endmodality(post)) 
       \replacewith(
            {#shadowed(#loc):=javaBitwiseXOrInt(#shadowed(#seCharByteShortInt0),
                                                #shadowed(#seCharByteShortInt1))}
                         \modality{#traonly}{.. ...}\endmodality(post)) 
       \heuristics(executeIntegerAssignment)
       \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seCharByteShortInt ^ #seLong; 
             ...}\endmodality(post)) 
      \replacewith(
           {#shadowed(#loc):=javaBitwiseXOrLong(#shadowed(#seCharByteShortInt),
					        #shadowed(#seLong))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLongTra2 { 
        \find (\modality{#traonly}{.. 
                   #loc=#seLong ^ #seCharByteShortInt; 
               ...}\endmodality(post)) 
        \replacewith (
             {#shadowed(#loc):=javaBitwiseXOrLong(#shadowed(#seLong),
                                                  #shadowed(#seCharByteShortInt))}
                  \modality{#traonly}{.. ...}\endmodality(post)) 
        \heuristics(executeIntegerAssignment)
        \displayname "bitwiseXOr"
  };

  assignmentBitwiseXOrLongTra3 { 
      \find (\modality{#traonly}{.. 
                 #loc=#seLong0 ^ #seLong1; 
             ...}\endmodality(post)) 
      \replacewith (
            {#shadowed(#loc):=javaBitwiseXOrLong(#shadowed(#seLong0),
					         #shadowed(#seLong1))}
                 \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "bitwiseXOr"
  };

  //------------- binary shift-right

  assignmentShiftRightIntTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seCharByteShortInt0 >> #se; 
              ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=javaShiftRightInt(#shadowed(#seCharByteShortInt0),
                                               #shadowed(#se))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"};

  assignmentShiftRightLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seLong0 >> #se; 
             ...}\endmodality(post)) 
      \replacewith (
           {#shadowed(#loc):=javaShiftRightLong(#shadowed(#seLong0),
                                                #shadowed(#se))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  //------------- binary shift-left

  assignmentShiftLeftIntTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seCharByteShortInt0 << #se; ...}
             \endmodality(post)) 
      \replacewith(
           {#shadowed(#loc):=javaShiftLeftInt(#shadowed(#seCharByteShortInt0),
                                              #shadowed(#se))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  assignmentShiftLeftLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seLong0 << #se; 
             ...}\endmodality(post)) 
      \replacewith (
            {#shadowed(#loc):=javaShiftLeftLong(#shadowed(#seLong0),
                                                #shadowed(#se))}
                 \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  //------------- binary unsigned-shift-right

  assignmentUnsignedShiftRightIntTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seCharByteShortInt0 >>> #se; ...}
             \endmodality(post)) 
      \replacewith (
            {#shadowed(#loc):=
                  javaUnsignedShiftRightInt(#shadowed(#seCharByteShortInt0),
		  			    #shadowed(#se))}
                \modality{#traonly}{.. ...}\endmodality(post)) 
      \heuristics(executeIntegerAssignment)
      \displayname "shift"
  };

  assignmentUnsignedShiftRightLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc=#seLong0 >>> #se; 
             ...}\endmodality(post)) 
     \replacewith ({#shadowed(#loc):=
     	  javaUnsignedShiftRightLong(#shadowed(#seLong0),
                                     #shadowed(#se))}
                    \modality{#traonly}{.. ...}\endmodality(post)) 
     \heuristics(executeIntegerAssignment)
     \displayname "shift"
  };


//------------------------------------------------------------------------
// ----------- Rules for unary plus, minus, and negation
//------------------------------------------------------------------------

  unaryMinusIntTra { 
     \find (\modality{#traonly}{.. 
                #loc = - #seCharByteShortInt; 
            ...}\endmodality(post)) 
     \replacewith(
           {#shadowed(#loc):=
                javaUnaryMinusInt(#shadowed(#seCharByteShortInt))}
            \modality{#traonly}{..  ...}\endmodality(post))
     \heuristics (executeIntegerAssignment)
     \displayname "unaryMinus"
  };

  unaryMinusLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc = - #seLong; 
             ...}\endmodality(post)) 
      \replacewith( {#shadowed(#loc):=javaUnaryMinusLong(#shadowed(#seLong))}
                        \modality{#traonly}{..  ...}\endmodality(post))
      \heuristics (executeIntegerAssignment)
      \displayname "unary_minus"
  };

  bitwiseNegationTra { 
      \find (\modality{#traonly}{.. #loc = ~ #se; ...}\endmodality(post)) 
      \replacewith({#shadowed(#loc):=javaBitwiseNegation(#shadowed(#se))}
          \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "invertBits"
  };


//------------------------------------------------------------------------
// ------------- Rules for Java types narrowing casts
//------------------------------------------------------------------------

  narrowingByteCastShortTra { 
     \find(\modality{#traonly}{..
               #loc = (byte) #seShort; 
           ...}\endmodality(post))
     \replacewith ({#shadowed(#loc) := 
                        javaCastByte(#shadowed(#seShort))}
           \modality{#traonly}{..  ...}\endmodality(post)) 
     \heuristics (executeIntegerAssignment)
     \displayname "cast" 
  };

  narrowingByteCastIntTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (byte) #seInt; 
             ...}\endmodality(post))
      \replacewith ({#shadowed(#loc) := javaCastByte(#shadowed(#seInt))}
           \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingByteCastLongTra { 
       \find (\modality{#traonly}{.. 
                  #loc = (byte) #seLong; 
              ...}\endmodality(post))
       \replacewith ({#shadowed(#loc) := 
                           javaCastByte(#shadowed(#seLong))}
                        \modality{#traonly}{..  ...}\endmodality(post)) 
       \heuristics (executeIntegerAssignment)
       \displayname "cast" 
  };

  narrowingShortCastIntTra { 
       \find (\modality{#traonly}{.. 
                  #loc = (short) #seInt; 
               ...}\endmodality(post))
       \replacewith ({#shadowed(#loc) := javaCastShort(#shadowed(#seInt))}
           \modality{#traonly}{..  ...}\endmodality(post)) 
       \heuristics (executeIntegerAssignment)
       \displayname "cast" 
  };

  narrowingShortCastLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (short) #seLong; 
             ...}\endmodality(post))
      \replacewith ({#shadowed(#loc):=javaCastShort(#shadowed(#seLong))}
            \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingIntCastLongTra { 
      \find (\modality{#traonly}{.. #loc = (int) #seLong; ...}\endmodality(post))
      \replacewith ({#shadowed(#loc):=javaCastInt(#shadowed(#seLong))}
                        \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastByteTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (char) #seByte; 
             ...}\endmodality(post))
      \replacewith (
           {#shadowed(#loc):=javaCastChar(#shadowed(#seByte))}
               \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastShortTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (char) #seShort; 
             ...}\endmodality(post))
      \replacewith (
            {#shadowed(#loc) := javaCastChar(#shadowed(#seShort))}
                 \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastIntTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (char) #seInt; 
             ...}\endmodality(post))
      \replacewith ({#shadowed(#loc):=javaCastChar(#shadowed(#seInt))}
           \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };

  narrowingCharCastLongTra { 
      \find (\modality{#traonly}{.. 
                 #loc = (char) #seLong; 
             ...}\endmodality(post))
      \replacewith ({#shadowed(#loc) := javaCastChar(#shadowed(#seLong))}
             \modality{#traonly}{..  ...}\endmodality(post)) 
      \heuristics (executeIntegerAssignment)
      \displayname "cast" 
  };
}
